import com.github.gradle.node.npm.task.NpmTask

plugins {
    id 'application'
    id 'groovy'
    id 'war'

    id 'org.grails.grails-web'
    id 'org.grails.grails-gsp'

    id 'com.github.erdi.webdriver-binaries'
    id 'com.github.node-gradle.node'
    id 'com.gorylenko.gradle-git-properties'
}

group = 'io.github.matrei'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.grails.org/grails/core' }
}

// This is used for testing with local copy of the Grails Inertia Plugin
// (comment out the dependency in the dependencies block)
// Also see section in settings.gradle
/*
grails {
    plugins {
        implementation project(':grails-inertia-plugin')
    }
}
*/

configurations.configureEach {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.codehaus.groovy') {
            String groovyVersion = project.findProperty('groovyVersion') ?: libs.versions.groovy.get()
            details.useVersion(groovyVersion)
        }
    }
}

dependencies {

    implementation 'org.grails:grails-core'
    implementation 'org.grails:grails-plugin-interceptors'
    implementation 'org.grails:grails-web-common'
    implementation 'org.grails:grails-web-databinding'

    implementation 'org.grails.plugins:gsp'
    implementation libs.grails.events
    implementation libs.grails.spring.security.core
    implementation libs.bundles.spring.security
    implementation libs.grails.gorm.logical.delete
    implementation libs.grails.inertia
    //implementation project(':grails-inertia-plugin')

    implementation libs.datafaker
    implementation libs.squiggly
    implementation libs.metadata.extractor
    implementation libs.imgscalr

    runtimeOnly 'org.grails:grails-plugin-i18n'
    runtimeOnly 'org.grails:grails-plugin-services'
    runtimeOnly 'org.grails:grails-plugin-url-mappings'
    runtimeOnly 'org.grails.plugins:hibernate5'
    runtimeOnly 'org.springframework.boot:spring-boot-autoconfigure'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-logging'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-tomcat'
    runtimeOnly libs.h2database
    runtimeOnly libs.hikari

    compileOnly 'io.micronaut:micronaut-inject-groovy'
    compileOnly 'org.grails.plugins:views-json'
    compileOnly libs.slf4j.nop // Get rid of warning about missing slf4j implementation during GSP compile tasks

    testImplementation 'io.micronaut:micronaut-http-client'
    testImplementation 'io.micronaut:micronaut-inject-groovy'
    testImplementation 'org.grails:grails-gorm-testing-support'
    testImplementation 'org.grails:grails-web-testing-support'
    testImplementation 'org.spockframework:spock-core'
    testImplementation libs.groovy.test

    integrationTestImplementation 'org.grails.plugins:geb'
    integrationTestImplementation libs.bundles.geb

    integrationTestRuntimeOnly libs.bundles.selenium
}

application {
    mainClass = 'pingcrm.Application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

tasks.withType(Test).configureEach {

    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }

    reports.html.required = false
    reports.junitXml.required = false

    systemProperty 'geb.env', System.getProperty('geb.env')
    systemProperty 'geb.build.reportsDir', reporting.file('geb/integrationTest')
    systemProperty 'webdriver.chrome.driver', "${System.getenv('CHROMEWEBDRIVER')}/chromedriver"
    systemProperty 'webdriver.gecko.driver', "${System.getenv('GECKOWEBDRIVER')}/geckodriver"
}

webdriverBinaries {
    chromedriver {
        version = '110.0.5481.77'
        fallbackTo32Bit = true
    }
    geckodriver = '0.32.2'
    edgedriver = '110.0.1587.57'
}

node {
    download = true
}

tasks.register('viteBuild', NpmTask) {
    group = 'build'
    description = 'Build the client bundle'

    inputs.dir(layout.projectDirectory.dir('node_modules'))
    inputs.dir(layout.projectDirectory.dir('src/main/javascript'))
    inputs.files(layout.projectDirectory.files('package.json', 'vite.config.js', 'tailwind.config.js', 'postcss.config.js', 'babel.config.js', 'jsconfig.json'))

    outputs.dir(layout.projectDirectory.dir('src/main/resources/public/dist'))

    it.args = ['run', 'build']

    dependsOn 'npmInstall'
}

tasks.register('copyNodeModulesForSsr', Copy) {
    group = 'build'
    description = 'Copy node_modules for SSR'

    from layout.projectDirectory.dir('node_modules')
    into layout.buildDirectory.dir('resources/main/node_modules')

    dependsOn 'npmInstall'
}

tasks.named('processResources') {
    dependsOn 'viteBuild', 'copyNodeModulesForSsr'
}